10 REM **** BOLOGNA & MILANO ****
20 KEY OFF:SCREEN0:BLOAD"BOLOGNA.BIN",S
30 COLOR 6,5:WIDTH 39
40 LOCATE12,10
50 PRINT"BOLOGNA & MILANO"
60 GOSUB 7690
70 COLOR15,4
80 CLS:LOCATE 0,10:PRINT"Bologna & Milano ‚ um jogo espetacular!"
90 GOSUB 7690
100 CLS
110 LOCATE  0,10:PRINT"Que vai testar sua capacidade p/administrar uma cidade, um estado ou uma na‡±o"
120 GOSUB 7690
130 DIM A(4)
140 DIM B(4)
150 DIM C(4)
160 DIM D(4)
170 DIM G(4)
180 DIM H(4)
190 DIM I(4)
200 DIM J(4)
210 DIM K(4)
220 DIM L(5)
230 DIM M(4)
240 DIM N(4)
250 DIM O(4)
260 DIM P(5)
270 DIM Q(4)
280 DIM R(4)
290 DIM S(4)
300 DIM T(5)
310 DIM U(5)
320 DIM V(5)
330 DIM Y(5)
340 DIM W(4)
350 DIM Z(4)
360 DIM A$(16)
370 DIM K$(4)
380 DIM N$(5)
390 DIM T$(5)
400 E=0
410 G1=0
420 O=0
430 Y(5)=1400
440 T$(1)="GENOVA"
450 T$(2)="MILANO"
460 T$(3)="VENEZA"
470 T$(4)="TORINO"
480 A$(1)="SENHOR "
490 A$(2)="BARAO "
500 A$(3)="CONDE "
510 A$(4)="MARQUES "
520 A$(5)="DUQUE "
530 A$(6)="GRAO-DUQUE "
540 A$(7)="PRINCIPE "
550 A$(8)="× REI × "
560 A$(9)="DAMA "
570 A$(10)="BARONESA "
580 A$(11)="CONDESSA "
590 A$(12)="MARQUESA "
600 A$(13)="DUQUESA "
610 A$(14)="GRAO-DUQUESA "
620 A$(15)="PRINCESA "
630 A$(16)="Ç RAINHA Á "
640 CLS
650 FOR K=1 TO 100 :NEXT:LOCATE  0,10:INPUT"Quantas pessoas (2 a 4) v±o participar";F$
660 F=VAL(F$)
670 CLS:BEEP
680 LOCATE  5,10:PRINT"Ir±o participar";F;"pessoas,OK?"
690 FOR K=1 TO  500:NEXT K
700 FOR A=1 TO F
710 CLS:BEEP
720 LOCATE 0,10:PRINT"Quem ‚ o governante de ";T$(A);:INPUT K$(A)
730 CLS
740 N$(A)=K$(A) + " de " + T$(A)
750 LOCATE 10,10:PRINT N$(A):PRINT:PRINT:PRINT:INPUT"     Homem (H) ou Mulher (M)";F$
760 IF F$="M" OR F$="m" THEN 810
770 IF F$="H" OR F$="h" THEN 790
780 GOTO 750
790 V(A)=0
800 GOTO 820
810 V(A)=8
820 CLS
830 G(A)=25
840 H(A)=10
850 I(A)=5
860 J(A)=2
870 Z=INT(RND(-TIME)*15)
880 O(A)=1420+Z
890 K(A)=1000
900 L(A)=10000
910 R(A)=5000
920 T(A)=1
930 U(A)=1
940 N(A)=4
950 P(A)=25
960 Q(A)=5
970 M(A)=25
980 S(A)=2000
990 NEXT A
1000 LOCATE 5,10:INPUT"Deseja ler instru‡”es  (S/N) ";F$
1010 IF F$="N"  OR F$="n" THEN 1030
1020 IF F$="S" OR F$="s" THEN 7120
1030 CLS:BEEP:LOCATE 7,5:PRINT" 0. D‚bil mental":LOCATE 7,7:PRINT" 1. Aprendiz"
1040 LOCATE 7,9:PRINT" 2. Aventureiro"
1050 LOCATE 7,11:PRINT" 3. Mestre"
1060 LOCATE 7,13:PRINT" 4. Grande mestre"
1070 LOCATE 7,15:PRINT" 5. Sagaz"
1080 LOCATE 5,2 :INPUT"Em que n¡vel deseja jogar";F$
1090 IF F$<"1"OR F$>"4"THEN 7630
1100 U(5)=VAL(F$)
1110 U(5)=U(5)+2+2*U(5)
1120 E=E+1
1130 IF E > F THEN 1240
1140 IF Z(E)<>1 THEN 1180
1150 W(E)=W(E)-1
1160 IF W(E)=-1 THEN 1180
1170 E=E+1
1180 IF T(E)=-1 THEN 1200
1190 GOTO 1210
1200 E=E+1
1210 IF T(1)<1 AND T(2)<1 AND T(3)<1 AND T(4)<1 THEN 6320
1220 IF E>F THEN 1240
1230 GOTO 1270
1240 Y(5)=Y(5)+1
1250 E=1
1260 GOTO 1130
1270 IF Y(5)>=O(E)THEN 1500
1280 GOSUB 1820
1290 GOSUB 2460
1300 GOSUB 3320
1310 GOSUB 2870
1320 GOSUB 6400
1330 GOSUB 4310
1340 GOSUB 5100
1350 GOSUB 5810
1360 GOTO 1120
1370 CLS
1380 PRINT:PRINT"NOB  SOL  CLE  COM  SERV  TERRA  CAIXA"
1390 PRINT"======================================"
1400 FOR A=1 TO F
1410 PRINT:PRINT TAB(15);T$(A)
1420 PRINT INT(N(A));TAB(4);INT(P(A));TAB( 9);INT(Q(A));TAB(14);INT(M(A));TAB(18);INT(S(A));TAB(25);INT(L(A));TAB(31);INT(K(A))
1430 NEXT
1440 LOCATE 8,16:PRINT"Digite <C> p/conven‡”es"
1450 LOCATE 8,17:PRINT"       <0> p/continuar"
1460 GOSUB 7090
1470 IF Z$="C" THEN GOSUB 5690
1480 IF Z$="0" THEN RETURN
1490 GOTO 1460
1500 CLS:LOCATE3,6:PRINT"ËËË NOTICIAS DESAGRADAVEIS ËËË"
1510 GOSUB 7760
1520 LOCATE 5,10:PRINT N$(E)
1530 PRINT"Acaba de falecer":FORK=1TO1000:NEXT
1540 T(E)=-1
1550 Y=INT(RND(2)*8)
1560 IF Y(5)<=1430 THEN 1590
1570 PRINT:PRINT"Devido a velhice ap¢s longo governo"
1580 GOTO 1640
1590 IF Y<4 THEN PRINT:PRINT"Devido a atentado terrorista do grupo pr¢ Napole„o"
1600 IF Y=5 THEN PRINT:PRINT"Devido a peste negra"
1610 IF Y=4 THEN PRINT:PRINT"Devido a febre tifoide"
1620 IF Y=6 THEN PRINT:PRINT"Devido a ataque de barbaros durante umaviagem"
1630 IF Y>6 THEN PRINT:PRINT"Devido a envenenamento alimentar"
1640 PRINT:PRINT "APERTE RETURN":FORK=1TO1000:NEXT
1650 INPUT Z$
1660 IF F=1 THEN 7260
1670 GOSUB 4310
1680 GOSUB 1370
1690 GOTO 1130
1700 K(E)=INT(K(E))
1710 RETURN
1720 Z=(RND(-TIME)*A)*S(E)/100
1730 Z2=INT(Z)
1740 PRINT:PRINT Z2;"servos nasceram este ano"
1750 S(E)=S(E)+Z2
1760 RETURN
1770 Z=(RND(-TIME)*A)*S(E)/100
1780 Z2=INT(Z)
1790 PRINT:PRINT Z2;"servos morreram este ano"
1800 S(E)=S(E)-Z2
1810 RETURN
1820 W=INT((INT(RND(-TIME)*5)+INT(RND(2) *6))/2)+1
1830 IF W=1 THEN 1880
1840 IF W=2 THEN 1900
1850 IF W=3 THEN 1920
1860 IF W=4 THEN 1940
1870 IF W=5 THEN 1960:IF W>5 THEN 1820
1880 W$="Estiagem - AMEA€A DE FOME !"
1890 GOTO 1970
1900 W$="Tempo ruim - COLHEITA POBRE"
1910 GOTO 1970
1920 W$="Tempo normal - COLHEITA RAZOAVEL"
1930 GOTO 1970
1940 W$="Tempo bom - COLHEITA BOA"
1950 GOTO  1970
1960 W$="Tempo ¢timo - COLHEITA EXCELENTE"
1970 R=INT(RND(-TIME)*50)
1980 IF R=0 THEN R=1
1990 R(E)=(R(E)*100-R(E)*R)/100
2000 X=INT (S(E)-D(E)*100)
2010 IF X<0 THEN X=1
2020 Y=W-.5
2030 DD=W*(S(E)-X)
2040 IF DD=0 THEN DD=1
2050 H1=INT (Y*L(E)/1.5+Y*X+(RND(-TIME)*S(E))-DD)
2060 R(E)=INT (R(E)+H1)
2070 D1=N(E)*100+C(E)*40+M(E)*30+P(E)*10+S(E)*5
2080 L=INT((100*W+INT(RND(-TIME)*9)+INT(RND(-TIME)*9))/10)
2090 L=(Y(5)-1400+L)/10
2100 Z=6-W
2110 G=(INT(RND(-TIME)*5)+INT(RND(-TIME)*5)+Z*10)/(5+2*Y)*30
2120 G=INT(G)
2130 RETURN
2140 PRINT:PRINT   "Os ratos comeram";R;" % da reserva  de      gr±os dos silos de armazenagem"
2150 PRINT:PRINT W$
2160 PRINT"Produ‡±o de gr±os";H1;"sacas"
2170 IF K(E)<32766 THEN GOSUB 1700
2180 PRINT:PRINT"RESERVA  DEMANDA  PRE€O   PRE€O  CAIXA gr±os    gr±os    gr±os   Terra":PRINT
2190 PRINT INT (R(E));TAB( 9);INT(D1);TAB(18);INT(G);TAB(26);L;TAB(32);INT(K(E))
2200 PRINT" sacas    sacas   mil/sc  hect. florins"
2210 RETURN
2220 J=(J(E)*300-500)*T(E)
2230 IF J(E)=1 THEN 2270
2240 IF J(E)=2 THEN 2290
2250 IF J(E)=3 THEN 2310
2260 IF J(E)=4 THEN 2330
2270 J$="Justa"
2280 GOTO 2340
2290 J$="Moderada"
2300 GOTO 2340
2310 J$="R¡spida"
2320 GOTO 2340
2330 J$="Abusiva"
2340 C1=(N(E)*90+Q(E)*35+M(E)*10)*(Y/100)+U(E)*20
2350 S1=(N(E)*50+M(E)*75+U(E)*10)*(Y/100)*(5-J(E))/2
2360 I1=N(E)*250+U(E)*20+(10*J(E)*N(E))*(Y/100)
2370 C1=INT(5*C1*G(E)/100)
2380 S1=INT(25*S1*H(E)/100)
2390 I1=INT(ABS(2*I1*I(E)/100))
2400 PRINT:PRINT "RENDA DO ESTADO:";J+C1+S1+I1;" florins"
2410 PRINT "ßßßßßßßßßßßßßßß":PRINT"TAXAS DA  TAXAS S/  IMPOSTOS  JUSTI€A  ALFANDEGA  VENDAS   DIVERSOS  (forma)"
2420 PRINT:PRINT TAB(1);G(E);"%";TAB(10);H(E);"%";TAB(21);I(E);"%";TAB(30);J$
2430 PRINT:PRINT TAB(1);C1;TAB(10);S1;TAB(21);I1;TAB(32);J
2440       PRINT "florins  florins    florins    florins"
2450 RETURN
2460 CLS
2470 LOCATE 0,1:PRINT A$(V(E) + T(E)); N$(E)
2480 GOSUB 2140
2490 PRINT"       OP€™ES ECONOMICAS"
2500 PRINT"       ßßßßßßßßßßßßßßßßß"       :PRINT"       1. Comprar gr±os":PRINT"       2. Vender gr±os"
2510 PRINT"       3. Comprar terras":PRINT"       4. Vender terras"
2520 PRINT:PRINT"       Area da na‡±o ==>";INT(L(E));"Hect.":PRINT"       Digite <0> p/continuar"
2530 GOSUB 7090
2540 IF Z$<>"0" AND Z$<>"1"AND Z$<>"2"AND Z$<>"3"AND Z$<>"4" THEN 2490
2550 I1=VAL(Z$)
2560 IF I1>4 THEN 2490
2570 IF I1<1 THEN RETURN
2580 IF I1=1 THEN 2620
2590 IF I1=2 THEN 2690
2600 IF I1=3 THEN 2760
2610 IF I1=4 THEN 2800
2620 CLS:LOCATE 5,10:INPUT" Quanto gr±o vai comprar";I1
2630 K(E)=K(E)-(I1*G/1000)
2640 R(E)=R(E)+I1
2650 CLS
2660 PRINT A$(T(E)+V(E));N$(E)
2670 GOSUB 2170
2680 GOTO 2490
2690 CLS:LOCATE 5,10:INPUT" Quanto gr±o vai vender";I1
2700 IF I1<=R(E)THEN 2730
2710 LOCATE 5,10:PRINT"N±o ha tanto gr±o dispon¡vel ...":GOSUB 8270:FORK=1TO 500:NEXT
2720 GOTO 2490
2730 K(E)=K(E)+(I1*G/1000)
2740 R(E)=R(E)-I1
2750 GOTO 2650
2760 CLS:LOCATE 5,10:INPUT"Quantos hect. vai comprar";I1
2770 L(E)=L(E)+I1
2780 K(E)=K(E)-(I1*L)
2790 GOTO 2650
2800 CLS:LOCATE 5,10:INPUT"Quantos hect. vai vender";I1
2810 IF I1<=(L(E)-5000)THEN 2840
2820 CLS:LOCATE 5,10:PRINT" Vocˆ n±o pode vender tanto!":GOSUB 8270:FORK=1TO500:NEXT
2830 GOTO 2800
2840 L(E)=L(E)-I1
2850 K(E)=K(E)+(I1*L)
2860 GOTO 2650
2870 CLS
2880 PRINT:PRINT A$(T(E) + V(E));N$(E)
2890 GOSUB 2220
2900 PRINT:PRINT"         OP€™ES FINANCEIRAS"
2910 PRINT"         ßßßßßßßßßßßßßßßßßß" :PRINT"         1. Taxas alfandegarias                 2. Taxas s/vendas"
2920       PRINT"         3. Impostos diversos                   4. Taxas judiciarias"
2930 PRINT:PRINT:PRINT"         Digite (0) p/continuar"
2940 GOSUB 7090
2950 IF Z$<>"0" AND Z$<>"1"AND Z$<>"2"AND Z$<>"3"AND Z$<>"4" THEN 2940
2960 I=VAL(Z$)
2970 IF I < 1 THEN 3210
2980 IF I = 1 THEN 3020
2990 IF I = 2 THEN 3070
3000 IF I = 3 THEN 3110
3010 IF I = 4 THEN 3150
3020 CLS:LOCATE 5,10:INPUT"  Nova taxa alfandegaria                      (0 A 100%)";I
3030 IF I>100 THEN I=100
3040 IF I<0 THEN I=0
3050 G(E)=I
3060 GOTO 2870
3070 CLS:LOCATE 5,10:INPUT"  Nova taxa s/vendas                          (0 A 50%)";I
3080 IF I>50 OR I<0 THEN I=5
3090 H(E)=I
3100 GOTO 2870
3110 CLS:LOCATE 5,10:INPUT"  Nova taxa s/impostos                        (0 A 25%)";I
3120 IF I<0 OR I>25 THEN I=0
3130 I(E)=I
3140 GOTO 2870
3150 CLS:LOCATE 3,10:PRINT"JUSTI€A ==>   1. Justa                               2. Moderada                            3. R¡spida                             4. Abusiva"
3160 GOSUB 7090
3170 IF Z$<>"1"AND Z$<>"2"AND Z$<>"3"AND Z$<>"4" THEN 3160
3180 I=VAL (Z$)
3190 J(E)=I
3200 GOTO 2870
3210 K(E)=K(E)+C1+S1+I1+J
3220 CLS
3230 IF K(E)>=0 THEN 3300
3240 JE=INT(RND(-TIME)*50)
3250 IF JE<20 THEN 3240
3260 LOCATE 5,10:PRINT"Os Bancos cobraram ";JE;"% de juros      sobre sua d¡vida"
3270 GOSUB 7760:FORK=1TO2000:NEXT
3280 JJ=1+JE/100
3290 K(E)=INT(K(E)*JJ)
3300 IF K(E)<(-10000*T(E)) THEN 6610
3310 RETURN
3320 CLS:LOCATE 5,10:INPUT" Quanto gr±o fornecera para  a po       pula‡±o da cidade";G1
3330 IF G1>=(R(E)/5)THEN 3360
3340 CLS:LOCATE 5,10:PRINT"   Vocˆ devera fornecer no m¡nimo         20% da reserva de gr±os":GOSUB 8270:FORK=1TO 500:NEXT
3350 GOTO 3320
3360 IF G1<=(R(E)-R(E)/5) THEN 3390
3370 CLS:LOCATE 5,10:PRINT"   Vocˆ devera manter 20% da reser        va de gr±os":GOSUB 8270:FORK=1TO 500:NEXT
3380 GOTO 3320
3390 R(E)=R(E)-G1
3400 CLS
3410 PRINT:PRINT A$(T(E) + V(E)); N$(E)
3420 Z=G1/D1-1
3430 IF Z>0 THEN Z=Z/2
3440 IF Z>.25 THEN Z=Z/10+.25
3450 Z2=50-G(E)-H(E)-I(E)
3460 IF Z2 <0 THEN Z2=Z2*J(E)
3470 Z2=Z2/10
3480 IF Z2>0 THEN Z2=Z2+3-J(E)
3490 Z=Z+(Z2/10)
3500 IF Z>.5 THEN Z=.5
3510 IF G1<(D1-1)THEN 3890
3520 A=7
3530 GOSUB 1720
3540 A=3
3550 GOSUB 1770
3560 IF (G(E)+H(E))<35 THEN M(E)=M(E)+INT(RND(-TIME )*8)
3570 IF I(E)<INT(RND(-TIME)*20) THEN N(E)=N(E)+INT (RND(2)*4)-1
3580 Q(E)=Q(E)+INT(RND(-TIME )*3)-1
3590 IF G1<(D1+D1*.3) THEN 3770
3600 Z2=S(E)/1000
3610 Z=(G1-D1)/D1*10
3620 Z=Z*Z2*INT(RND(-TIME) *25)+INT(RND(2)*40)
3630 IF Z>32000 THEN Z=32000
3640 Z2=Z
3650 Z=INT(RND(-TIME) *Z2)
3660 IF Z<1 THEN Z=2
3670 PRINT:PRINT Z;"servos mudaram para sua cidade"
3680 GOSUB 7760
3690 S(E)=S(E)+Z
3700 U(E)=U(E)+.5
3710 Z2=Z/5
3720 Z = INT(RND(-TIME) *Z2)
3730 IF Z>50 THEN Z=50
3740 M(E)=M(E)+Z
3750 N(E)=N(E)+1
3760 Q(E)=Q(E)+2
3770 IF J(E)<3 THEN 3880
3780 J1=S(E)/100*(J(E)-2)*(J(E)-2)
3790 J1=INT(RND(-TIME)*J1)
3800 IF J1<=1 THEN J1=2
3810 S(E)=S(E)-J1
3820 PRINT:PRINT:PRINT J1;"servos fugiram em virtude de injus-     ti‡as":GOSUB 7760
3830 PRINT:PRINT"     Digite (0) p/continuar"
3840 GOSUB 7090
3850 IF Z$="0"THEN 3870
3860 GOTO 3840
3870 CLS
3880 GOTO 4030
3890 X=(D1-G1)/D1*100-9
3900 X2=X
3910 IF X <=65 THEN 3940
3920 X=65
3930 M(E)=M(E)/2
3940 IF X>=0 THEN 3970
3950 X2=0
3960 X=0
3970 A=3
3980 GOSUB 1720
3990 A=X2+8
4000 GOSUB 1770
4010 IF Z2>1000 THEN U(E)=U(E)/2
4020 GOTO 3770
4030 HE=H(E)
4040 IF H(E)<=5 THEN HE=INT(RND(-TIME) *4+1)
4050 Z=INT (A(E)*S(E)*M(E)/(1500*SQR(HE)))
4060 IF Z>=A(E)*1000 THEN Z=INT(A(E)*300*(1+RND(-TIME) *3))
4070 K(E)=K(E)+Z
4080 IF Z<=0 THEN 4100
4090 PRINT:PRINT"Seus mercados renderam ";Z;" florins"
4100 IF D(E)=0 THEN 4180
4110 TX=R(E)/(D(E)*5000)
4120 IF TX>1 THEN TX=1
4130 Z=INT (D(E)*TX*S(E)/(3*SQR(HE)))
4140 IF Z>=D(E)*2000 THEN Z=INT((D(E)*1000*(1+RND(-TIME )*2))
4150 R(E)=R(E)-TX*D(E)*5000
4160 K(E)=K(E)+Z
4170 PRINT:PRINT"Seus moinhos renderam";Z;" florins"
4180 Z=P(E)*3
4190 PRINT:PRINT"Gastos militares: ";Z;" florins"
4200 K(E)=K(E)-Z
4210 IF (L(E)/1000)>P(E) THEN 6790
4220 IF (L(E)/500)<P(E) THEN 4270
4230 FOR A=1 TO F
4240 IF A=E THEN 4260
4250 IF P(A)>(P(E)*2.4) THEN 6790
4260 NEXT
4270 LOCATE  2,18:LINE INPUT "P/continuar press. <0> e <RETURN>";H$
4280 IF H$="0"THEN 4300
4290 GOTO 4270
4300 RETURN
4310 CLS:L2=L(E)/1000
4320 LOCATE 14,2:PRINT T$(E)
4330 LOCATE 25,2:PRINT L(E);"hect."
4340 IF (P(E)-5)<(L(E)/1000) THEN 4450
4350 LOCATE 1,6:PRINT" ÛÛÛÛÛÛÛ"
4360 LOCATE 1,7:PRINT"  ÛËËËÛ"
4370 LOCATE 1,8:PRINT"  ÛËËËÛ"
4380 LOCATE 1,9:PRINT"  ÛËËËÛ"
4390 LOCATE 1,10:PRINT" ÛÛÛUÛÛÛ"
4400 IF (P(E)/2)<(L(E)/1000)THEN 4480
4410 LOCATE 1,4:PRINT" Â Â Â Â"
4420 LOCATE 1,5:PRINT" ÛÛÛÛÛÛÛ"
4430 LOCATE 1,6:PRINT"  ÛÛÛÛÛ "
4440 LOCATE 1,7:PRINT"  ÛËËËÛ"
4450 LOCATE 1,8:PRINT"  ÛËËËÛ"
4460 LOCATE 1,9:PRINT" ÛÛÛÛÛÛÛ"
4470 LOCATE 1,10:PRINT" ÛÛÛUÛÛÛ"
4480 Z=C(E)
4490 IF Z=0 THEN 4650
4500 IF Z>7 THEN Z=7
4510 IF Z=1 THEN 4640
4520 IF Z=2 THEN 4630
4530 IF Z=3 THEN 4620
4540 IF Z=4 THEN 4610
4550 IF Z=5 THEN 4600
4560 IF Z=6 THEN 4590
4570 IF Z=7 THEN 4580
4580 LOCATE 26,14:PRINT"ÎÎUÎÎ"
4590 LOCATE 26,15:PRINT"ÛÅÅÅÛ"
4600 LOCATE 26,16:PRINT"Ý×××Ê"
4610 LOCATE 26,17:PRINT"Ý×××Ê"
4620 LOCATE 26,18:PRINT"Ý×××Ê"
4630 LOCATE 26,19:PRINT"Ý×××Ê"
4640 LOCATE 26,20:PRINT"ÛÛÀÛÛ"
4650 Z=B(E)
4660 IF Z=0 THEN 4810
4670 IF Z=1 THEN 4800
4680 IF Z=2 THEN 4790
4690 IF Z=3 THEN 4780
4700 IF Z=4 THEN 4770
4710 IF Z=5 THEN 4760
4720 IF Z=6 THEN 4750
4730 IF Z=7 THEN Z=7
4740 LOCATE 20,14:PRINT" ÎÎÎ "
4750 LOCATE 20,15:PRINT"ÎÅÅÅÎ"
4760 LOCATE 20,16:PRINT"ÝÌÌÌÊ"
4770 LOCATE 20,17:PRINT"ÝÌÌÌÊ"
4780 LOCATE 20,18:PRINT"ÝÌÌÌÊ"
4790 LOCATE 20,19:PRINT"ÝÌÌÌÊ"
4800 LOCATE 20,20:PRINT"ÛÛUÛÛ":GOTO 4820
4810 LOCATE 20,20:PRINT"©ÍÍÍª"
4820 GOSUB 5060
4830 LOCATE XX,YY:PRINT"©Åª"
4840 LOCATE XX,YY-1:PRINT" O "
4850 LOCATE XX,YY-2:PRINT" Î "
4860 Z=A(E)
4870 IF Z=0 THEN 4930
4880 LOCATE 8,17:PRINT"ÎÎMÎÎ"
4890 LOCATE 8,18:PRINT"ÛßßßÛ"
4900 LOCATE 8,19:PRINT"ÝÌÌÌÊ"
4910 LOCATE 8,20:PRINT"ÛÛ×ÛÛ"
4920 LOCATE  9,19:PRINT Z
4930 Z=D(E)
4940 IF Z=0 THEN 5020
4950 LOCATE 14,15:PRINT"ÎÎÎÎÎ"
4960 LOCATE 14,16:PRINT"ÛÛÛÛÛ"
4970 LOCATE 14,17:PRINT"Ý\\\Ê"
4980 LOCATE 14,18:PRINT"Ý\\\Ê"
4990 LOCATE 14,19:PRINT"Ý\À\Ê"
5000 LOCATE 14,20:PRINT"ÛÛUÛÛ"
5010 LOCATE 15,18:PRINT Z
5020 LOCATE 1,3:PRINT"ANO";Y(5)
5030 LOCATE 3,24:PRINT "   Qualquer tecla p/continuar "
5040 GOSUB 7090
5050 RETURN
5060 Z=INT((A(E)+B(E)+C(E)+D(E)+K(E)/1000+L(E)/1000+R(E)/10000+S(E)/1000/2))
5070 IF Z<10 THEN Z=10
5080 IF Z>35 THEN Z=35
5090 XX=35:YY=INT((54-Z)/2):RETURN
5100 CLS
5110 LOCATE 0,0:PRINT A$(T(E) + V(E));N$(E)
5120       PRINT:PRINT"INVESTIMENTOS DO ESTADO:"
5130 PRINT"ßßßßßßßßßßßßßßßßßßßßßßß":PRINT"1. Mercados   (1000 florins,cada)"
5140 PRINT"2. Moinhos    (2000 florins,cada)"
5150 PRINT"3. Palacios   (parte - 3000 florins)"
5160 PRINT"4. Catedrais  (parte - 5000 florins)"
5170 PRINT"5. Gastos militares (forma‡„o de um pe-              lot±o - 500 florins)"
5180 PRINT:PRINT"DISPONIBILIDADE CAIXA:";INT(K(E));"florins":                      PRINT"ßßßßßßßßßßßßßßßßßßßßßß"
5190 IF K(E)<-30000 THEN 6610
5200 PRINT:PRINT"(Que Investimento fara?)":PRINT:PRINT:PRINT"Press. <6> p/comparar                         <7> p/mapear"
5210 PRINT"       <0> p/continuar"
5220 GOSUB 7090
5230 IF Z$<>"0" AND Z$<>"1" AND Z$<>"2"AND Z$<>"3"AND Z$<>"4"AND Z$<>"5"AND Z$<>"6"AND Z$<>"7" THEN 5220
5240 I=VAL(Z$)
5250 IF I>=1 THEN 5280
5260 CLS
5270 RETURN
5280 IF I=1 THEN 5440
5290 IF I=2 THEN 5380
5300 IF I=3 THEN 5510
5310 IF I=4 THEN 5580
5320 IF I=5 THEN 5650
5330 IF I=6 THEN 5360
5340 GOSUB 4310
5350 GOTO  5100
5360 GOSUB 1370
5370 GOTO  5100
5380 CLS:LOCATE 2,10:INPUT"Quantos moinhos deseja comprar ";I
5390 D(E)=D(E)+I
5400 K(E)=K(E)-I*2000
5410 IF K(E)<= -30000 THEN 6610
5420 U(E)=U(E)+I*.25
5430 GOTO 5100
5440 CLS:LOCATE 2,10:INPUT"Quantos mercados deseja comprar ";I
5450 A(E)=A(E)+I
5460 IF K(E) <= -30000 THEN 6610
5470 M(E)=M(E)+2
5480 K(E)=K(E)-I*1000
5490 U(E)=U(E)+I*.1
5500 GOTO 5100
5510 CLS:LOCATE 2,10:INPUT"Quantos palacios deseja comprar";I
5520 B(E)=B(E)+I
5530 K(E)=K(E)-I*3000
5540 IF K(E) <= -30000 THEN 6610
5550 N(E)=INT(N(E)+RND(-TIME)*I*2)
5560 U(E)=U(E)+I*.5
5570 GOTO 5100
5580 CLS:LOCATE 2,10:INPUT"Quantas catedrais deseja comprar ";I
5590 K(E)=K(E)-I*5000
5600 IF K(E)<=-30000 THEN 6610
5610 Q(E)=INT(Q(E)+RND(-TIME)*6*I)
5620 C(E)=C(E)+I
5630 U(E)=U(E)+I
5640 GOTO 5100
5650 P(E)=P(E)+20
5660 S(E)=S(E)-20
5670 K(E)=K(E)-500
5680 GOTO 5100
5690 CLS
5700 PRINT:PRINT"          DD CONVENC™ES DD"
5710 PRINT:PRINT:PRINT"       NOB............Nobres"
5720 PRINT:PRINT"       SOL............Soldados"
5730 PRINT:PRINT"       CLE............Clero"
5740 PRINT:PRINT"       COM............Comerciantes"
5750 PRINT:PRINT"       SER............Servos"
5760 PRINT:PRINT"       CAIXA..........Dinheiro"
5770 PRINT:PRINT:PRINT"        Digite (0) p/voltar "
5780 GOSUB 7090
5790 IF Z$="0" THEN 1370
5800 GOTO 5780
5810 Z=0
5820 A=A(E)
5830 GOSUB 6280
5840 A=B(E)
5850 GOSUB 6280
5860 A=C(E)
5870 GOSUB 6280
5880 A=D(E)
5890 GOSUB 6280
5900 A=K(E)/5000
5910 GOSUB 6280
5920 A=(L(E)-5000)/4000
5930 GOSUB 6280
5940 A=M(E)/50
5950 GOSUB 6280
5960 A=N(E)/5
5970 GOSUB 6280
5980 A=P(E)/50
5990 GOSUB 6280
6000 A=Q(E)/10
6010 GOSUB 6280
6020 A=S(E)/2000
6030 GOSUB 6280
6040 A=U(E)/5
6050 GOSUB 6280
6060 A=INT (Z/U(5)-J(E)+1)
6070 IF A>8 THEN A=8
6080 IF (Y(5)+2)<>O(E) THEN 6110
6090 T(E)=T(E)+1
6100 GOTO 6130
6110 IF T(E)>= A GOTO 6270
6120 T(E)=A
6130 IF T(E)=8 THEN 6310
6140 CLS
6150 LOCATE 5,10:PRINT"NNËËË  CONGRATULA€™ES ËËËNN"
6160 PRINT:PRINT:PRINT N$(E)
6170 PRINT:PRINT   "Vocˆ agora ‚ ";A$ (T(E) + V(E))
6180 N$(E)=K$(E)+ " de " + T$(E)
6190 PLAY"CDEFGABR2","L8CDEFGABR2":PLAY"T240L6V12","T240L2V9":PLAY"R8O6GABO7DCCEB","O4GO5GE"
6200 PLAY"DG#GDO6BGAB","O4BO5EO4E"
6210 PLAY"O7CDEDCO6BABG","O4ABO5C"
6220 PLAY"F#GADF#AO7CO6BC","O5DF#D"
6230 PLAY"BGABO7DCCED","GGC"
6240 PLAY"DGF#DO6BGAB","O4BO5ED"
6250 PLAY"EO7DCO6BAGDGF#G2","CC#DG"
6260 FOR K=1 TO 1000 :NEXT
6270 RETURN
6280 IF A>10 THEN A=10
6290 Z=Z+A
6300 RETURN
6310 CLS
6320 LOCATE 10,10:PRINT"ÌÌÌ GAME OVER ËËË"
6330 FOR K=1 TO 1000:NEXT
6340 CLS:LOCATE  7,10:PRINT  A$(T(E) + V(E));N$(E)
6350 PRINT:PRINT:PRINT "      ÁÁÁÁÁÁ     VENCEU    ÁÁÁÁÁÁÁ"
6360 GOSUB 7960
6370 GOSUB 4310
6380 GOSUB 1370
6390 GOTO 7260
6400 PEST = INT(RND(2)*100)
6410 IF PEST=INT((RND(-TIME)*1000)/10)THEN 6430
6420 RETURN
6430 CLS
6440 PP=INT(RND(-TIME)*70)
6450 NN=INT (N(E)*PP/100)+1
6460 N(E)=N(E)-NN
6470 CC=INT(Q(E)*PP/100)+2
6480 Q(E)=Q(E)-CC
6490 MM=INT (M(E)*PP/100)+2
6500 M(E)=M(E)-MM
6510 SS=INT(S(E)*PP/100)+2
6520 S(E)=S(E)-SS
6530 PRINT:PRINT" ËËËËù NOTICIAS CATASTROFICAS ùËËËËË"
6540 GOSUB 7760
6550 PRINT:PRINT"A peste negra varreu sua cidade vitimando:"
6560 PRINT NN;"   nobres +"
6570 PRINT CC;"   bispos e padres +"
6580 PRINT MM;"   comerciantes +"
6590 PRINT SS;"   servos +"
6600 FOR G=1 TO 5000:NEXT:RETURN
6610 CLS
6620 LOCATE 4,10:PRINT A$(T(E) + V(E));N$(E):PRINT:PRINT:PRINT       "ÌÌÌ  FALIU ÌÌÌ"
6630 GOSUB 7760:FORK=1TO5000:NEXT
6640 IF K(E)<=(-50000!*T(E)/3) THEN 7310
6650 CLS:LOCATE 2,10:PRINT" Os Bancos tomaram seus bens"
6660 GOSUB 7760
6670 CLS
6680 A(E)=INT(A(E)*(INT(RND(-TIME)*10)/10))
6690 B(E)=INT(B(E)*(INT(RND(-TIME)*10)/10))
6700 C(E)=INT(C(E)*(INT(RND(-TIME)*10)/10))
6710 D(E)=INT(D(E)*(INT(RND(-TIME)*10)/10))
6720 L(E)=INT(L(E)*(INT(RND(-TIME)*10)/10))
6730 U(E)=1
6740 K(E)=100
6750 M(E)=INT(M(E)*INT(RND(-TIME)*7)/10)
6760 R(E)=R(E)-5000
6770 IF L(E) <5000 THEN L(E)=5000
6780 RETURN
6790 Z=5
6800 FOR A=1 TO F
6810 IF A=6 THEN 6850
6820 IF P(A) <P(E) THEN 6850
6830 IF P(A) <(1.2*(L(A)/1000)) THEN 6850
6840 IF P(A) >P(Z) THEN Z=A
6850 NEXT
6860 IF Z=5 THEN T$(5) = "BAR°O "
6870 N$(5)="MALONE DE VINCENZA"
6880 A1=INT(RND(-TIME)*9000+1000)
6890 GOTO  6910
6900 A1=P(Z)*1000-L(Z)/3
6910 IF A1 >(L(E)-5000) THEN A1=(L(E)-5000)/2
6920 PRINT:PRINT"     Digite <0> p/continuar "
6930 GOSUB 7090
6940 IF F=1 THEN 6980
6950 CLS
6960 LOCATE 5,10:PRINT A$(T(Z)+V(Z));N$(Z);" invadiu e anexou ";A1; "hectares de terras"
6970 GOTO 7000
6980 CLS
6990 LOCATE 5,10:PRINT A$(Z);N$(Z);" invadiu e anexou ";A1;"hectares de terras"
7000 L(Z)=L(Z)+A1
7010 L(E)=L(E)-A1
7020 Z=INT(RND(-TIME)*40)
7030 IF Z>(P(E)-15) THEN Z=P(E)-15
7040 LOCATE 5,13:PRINT A$(T(E)+V(E));""    ;N$(E);" perdeu";Z;" soldados em batalha"
7050 P(E)=P(E)-Z
7060 PRINT:PRINT "Aperte <RETURN>"
7070 INPUT Z$
7080 RETURN
7090 Z$=INKEY$
7100 IF Z$="" THEN 7090
7110 RETURN
7120 CLS
7130 LOCATE 10,4:PRINT"ÌÌÌINSTRU€™ESÌÌÌ"
7140 LOCATE 0,10:PRINT"A partir de agora  vocˆ sera o governante de uma cidade-estado do s‚culo  XV.":GOSUB 7900
7150 CLS:LOCATE 0,10:PRINT"A cada per¡odo de bom governo vocˆ recebera t¡tulos cada vez maiores.":GOSUB 7900
7160 CLS:LOCATE 0,10:PRINT"A expectativa de vida naquela ‚poca eramuito curta, logo vocˆ dispora de poucotempo para governar.":GOSUB 7900
7170 CLS:LOCATE 0,10:PRINT"Quem primeiro chegar a REI ou RAINHA sera o vencedor do jogo.":GOSUB 7900
7180 CLS:LOCATE 0,10:PRINT"O tamanho da torre no canto superior esquerdo indicara se suas defesas s±o adequadas.":GOSUB 7900
7190 CLS:LOCATE 0,10:PRINT"O boneco em posi‡±o alta significa ter-ras em franca produ‡±o, caso contrario vocˆ precisara de mais servos.":GOSUB 7900
7200 CLS:LOCATE 0,10:PRINT"Uma boa distribui‡±o de gr±os a popula-‡±o, aumentar  a taxa de natalidade  e incentivara a migra‡±o de novos servos.":GOSUB 7900
7210 CLS:LOCATE 0,10:PRINT"Taxas e impostos elevados,  aumentam  aarrecada‡±o mas afetam a economia da na‡±o.":GOSUB 7900
7220 CLS:LOCATE 0,10:PRINT"Fa‡a um governo democratico e lembre-seque ‚ sempre bom comprar na baixa  paravender na alta.":GOSUB 7900
7230 FOR K=1 TO 4250:NEXT:CLS:LOCATE 10,10:PRINT" BOA  SORTE !"
7240 GOSUB 8270
7250 GOTO 1030
7260 CLS:LOCATE 10,10:PRINT" ÌÌÌ GAME OVER ÌÌÌ"
7270 GOSUB 8300
7280 LOCATE 2,10:INPUT"Quer jogar novamente (S/N)";F$
7290 IF F$="S" OR F$="s" THEN 8330
7300 IF F$="N" OR F$="n" THEN GOTO  7660
7310 CLS:LOCATE 2,10:PRINT"Os Bancos confiscaram seus bens..." :GOSUB 7760:FORK=1TO3000:NEXT
7320 IF K(E) <= -20000 AND K(E) > -35000! THEN 7350
7330 IF K(E) <= -35000! AND K(E)>-50000! THEN 7370
7340 IF K(E) <= 50000! THEN 7400
7350 W(E)=W(E)+1
7360 GOTO 7430
7370 W(E)=W(E)+INT(RND(-TIME)*4)
7380 IF W(E)=0 THEN W(E)=1
7390 GOTO 7430
7400 W(E)=W(E)+INT(RND(-TIME)*6)
7410 IF W(E) <2 THEN W(E)=3
7420 GOTO 7430
7430 G$="o"
7440 IF V(E)=8 THEN G$="a"
7450 CLS:LOCATE 4,10:PRINT A$(T(E)+V(E));N$(E)
7460 PRINT"    Foi a julgamento,acusad";G$;" de fraude"
7470 PRINT:PRINT"    Desta forma foi condenad";G$;" a ";W(E);"anos    de pris„o"
7480 GOSUB 8270:FORK=1TO5000:NEXT
7490 IF F=1 THEN 7520
7500 IF F>1 THEN Z(E)=1
7510 GOTO 7530
7520 Y(5)=Y(5)+W(E)
7530 A(E)=0
7540 B(E)=0
7550 C(E)=0
7560 D(E)=0
7570 L(E)=4000
7580 U(E)=1
7590 K(E)=0
7600 M(E)=INT (M(E)/4)
7610 R(E)=5000
7620 GOTO 6780
7630 CLS
7640 LOCATE 2,10:PRINT"Desista... Este jogo n±o ‚ para vocˆ!":FOR K=1 TO 1000:NEXT:GOTO 7940
7650 GOTO 1030
7660 CLS:BEEP:LOCATE 10,10:PRINT"    F I M "
7670 GOSUB 7760:FOR K=1 TO 1000:NEXT
7680 CLS:END
7690 PLAY"O2L16BO3BBBO4CO3BABBF#B","O3L8O2BO3BO4CO3ABF#"
7700 PLAY"O2L16BO3BBBO4CO3BABBF#B","O3L8O2BO3BO4CO3ABF#"
7710 PLAY"O2L16BO3BF#BABBBO4CO3BO4DO3B","O2L8BO3F#ABO4CD"
7720 PLAY"O3L16BBO4CO3BABBBGBF#B","O3L8BO4CO3ABGF#","O2L2B."
7730 PLAY"O2L16BO3BBBO4CO3BABBBF#B","O2L8BO3O4CO3ABF#","O2L2B."
7740 PLAY"O2L16BO3BBBO4CO3BABBBF#B","O2L8BO3O4CO3ABF#","O2L2B."
7750 RETURN
7760 PLAY"S0M10000V15T180"
7770 PLAY"L4O3GFL8EDECO2BABG"
7780 PLAY"O3L4CEAGBO4L3CO3"
7790 PLAY"L8AGFEL3EL4DGBO4C"
7800 FOR K=1 TO 1000:NEXT:RETURN
7810 PLAY"R8O6GABO7DCCEB","O4GO5GE"
7820 PLAY"DG#GDO6BGAB","O4BO5EO4E"
7830 PLAY"O7CDEDCO6BABG","O4ABO5C"
7840 PLAY"F#GADF#AO7CO6BA","O5DF#D"
7850 PLAY"BGABO7DCCED","GGC"
7860 PLAY"DGF#DO6BGAB","O4BO5ED"
7870 PLAY"EO7DCO6BAGDGF#G2","CC#DG"
7880 FOR K=1 TO 1000:NEXT
7890 RETURN
7900 PLAY"M2000S11T255O3CCEEGGAAA#A#AAGGEE"
7910 PLAY"FFAAO4CCDDD#D#DDCCO3AA"
7920 PLAY"GGBBO4DDEEO3FFAAO4CCDD"
7930 RETURN
7940 SOUND 7,56:SOUND 8,15:SOUND 1,0
7950 SOUND 0,50:FOR I=1 TO 200:NEXT:SOUND 0,100:FOR I=1 TO 200:NEXT I:GOTO 7650
7960 PLAY"v10t180","v9t180"
7970 PLAY"o4l8deL4f#g","l4o1do2ao3d"
7980 PLAY"o4l2al4bgea","l4o1do2ao3do1do2ao3d"
7990 PLAY"o4l2f#.l8edef#ge","o1l4do2ao3do1do2ao3d"
8000 PLAY"o4l2f#gl4e.l8f#","o1l4do2ao3do1do2ao3d"
8010 PLAY"o4l2d..l8el4f#g","l4o1do2ao3do1do2ao3d"
8020 PLAY"o4l4aabgea","l4o1do2ao3do1do2ao3d"
8030 PLAY"o4l2f#.l8edef#ge","l4o1do2ao3do1do2ao3d"
8040 PLAY"o4l8f#f#gf#edc#o3bo4c#def#","l4o1do2ao3do1do2ao3d"
8050 PLAY"o4l2d.l4f#ed","l4o1do2ao3do1do2ao3d"
8060 PLAY"o4l1f#l4ed","l4o1Do2ao3Do1Do2ao3d"
8070 PLAY"o4l2a.l4f#ed","l4o1do2ao3do1do2ao3d"
8080 PLAY"o4l2agl8eeef#","l4o1do2ao3do1do2ao3d"
8090 PLAY"o4l2d.l8gf#l4ed","l4o1do2ao3do1do2ao3d"
8100 PLAY"oo4l2f#.l8gf#l4ed","l4o1do2ao3do1do2ao3d"
8110 PLAY"o4l2a.l8edef#ge","l4o1do2ao3do1do2ao3d"
8120 PLAY"o4l8f#f#ef#gf#edc#ded#","o1l4do2ao3do1do2ao3d"
8130 PLAY"o4l2d.l8o3del4f#g","l4o1do2ao3do1do2ao3d"
8140 PLAY"o3l4aabgea","l4o1do2ao3do1do2ao3d"
8150 PLAY"o3l2f#.l8edef#ge","l4o1do2ao3do1do2ao3d"
8160 PLAY"o3l8f#f#gf#edc#o2bo3c#def#","l1o1d."
8170 PLAY"o3l2d.l4f#ed","l4o1do2ao3do1do2ao3d"
8180 PLAY"o3l2f#.l4f#ed","l4o1do2ao3do1do2ao3d"
8190 PLAY"o3l2a.l4f#ed","l4o1do2ao3do1do2ao3d"
8200 PLAY"o3l4al8gf#edc#o2bo3c#def#","l1o1d."
8210 PLAY"o3l2d.o4l8gf#l4ed","l4o1do2ao3do1do2ao3d"
8220 PLAY"o4l2f#.l8gf#ef#dd","l4o1do2ao3do1do2ao3d"
8230 PLAY"o4l2a.l8gf#ef#ge","l4o1do2ao3do1do2ao3d"
8240 PLAY"o4l8f#gaf#gagf#edef#","o1l4do2ao3do1do2ao3d"
8250 PLAY"l1d","l4o1do2ao3do1d"
8260 RETURN
8270 DATA 100,125,112,170,170,112,100,125
8280 RESTORE:SOUND 7,56:SOUND 8,16:SOUND 1,0:SOUND 12,36:FOR I=1 TO 8:READ A:SOUND 0,A:SOUND 13,0:FOR P=1 TO 150:NEXT P:IF I=4 THEN FOR P=1 TO 150:NEXT P:NEXT I ELSE NEXT I
8290 RETURN
8300 SOUND 7,56:SOUND 8,15:SOUND 1,0:SOUND 9,15:SOUND 3,0
8310 SOUND 0,190:SOUND 2,192:FOR I=1 TO 400:NEXT I:SOUND 0,250:SOUND 2,253:FOR I=1 TO 400:NEXT I
8320 RETURN
8330 SOUND 7,56:SOUND 8,15:SOUND 1,0
8340 FOR L=230 TO 100 STEP -.5:SOUND 0,L:NEXT L:SOUND 0,0:FOR I=1 TO 150:NEXT I
8350 GOTO 640
