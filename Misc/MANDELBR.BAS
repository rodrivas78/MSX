10 ' Adapted from https://rosettacode.org/wiki/Mandelbrot_set
200 CLEAR 200,&HD800:COLOR15,1,1:CLS
202 PRINT"Press a key from 1 to 4"
204 PRINT"to select a view.":PRINT
206 PRINT"When finished press any"
208 PRINT"key to change palettes."
210 I$=INPUT$(1)
215 OP=VAL(I$)
220 IF OP<1 OR OP>4 GOTO 210
225 POKE&HD800,OP
230 COLOR,0,0:SCREEN 3
235 _TURBO ON
240 W=63:H=47:IT=50:OP=PEEK(&HD800)
245 IF OP=1 THEN XL=-2:XH=1:YL=-1:YH=1
250 IF OP=2 THEN XL=-2:XH=-.5:YL=-.5:YH=.5
255 IF OP=3 THEN XL=-1.4:XH=-.9:YL=-.5:YH=-.16
260 IF OP=4 THEN XL=-.4:XH=.1:YL=.8:YH=1.13
265 FOR IX=0 TO W:FOR IY=0 TO H
270 X=XL+(XH-XL)*IX/W:Y=YL+(YH-YL)*IY/H
275 CX=X:CY=Y:MI=IT
280 MX=0:MY=0:XX=0:YY=0:XY=0:I=0
285 XY=MX*MY:XX=MX*MX:YY=MY*MY
290 MX=XX-YY+CX:MY=XY+XY+CY
295 I=I+1
300 IF I<MI AND XX+YY<=4 GOTO 285
305 IF I=IT THEN C=0:GOTO 315
310 C=INT(16*LOG(I)/LOG(IT-1))-1
315 PSET(IX*4,IY*4),C
320 NEXT IY:NEXT IX
325 _TURBO OFF
330 NP=2
335 DIM R(NP,15):DIM G(NP,15):DIM B(NP,15)
340 FOR P=0 TO NP:FOR C=0 TO 15
345 READ R(P,C),G(P,C),B(P,C)
350 NEXT C,P
355 FOR P=0 TO NP:FOR C=0 TO 15
360 COLOR=(C,R(P,C),G(P,C),B(P,C))
365 NEXT C:I$=INPUT$(1):NEXT P
370 GOTO 355
375 'FIRE
380 DATA 0,0,0,1,0,0,2,0,0,3,0,0,4,0,0,5,0,0,6,0,0,7,0,0
385 DATA 7,1,0,7,2,0,7,3,0,7,4,0,7,5,0,7,6,0,7,7,0,7,7,7
390 'WIND
395 DATA 0,0,0,0,0,1,0,0,2,0,0,3,0,0,4,0,0,5,0,0,6,0,0,7
400 DATA 0,1,7,0,2,7,0,3,7,0,4,7,0,5,7,0,6,7,0,7,7,7,7,7
405 'EARTH
410 DATA 0,0,0,0,1,0,0,2,0,0,3,0,0,4,0,0,5,0,0,6,0,0,7,0
415 DATA 1,7,0,2,7,0,3,7,0,4,7,0,5,7,0,6,7,0,7,7,0,7,7,7
